var panic_flag = false
var Timer timer = null

rule "Initializing phase"
when
	System started
then
	say("Добро пожаловать в умный дом.")
	sendTelegram("smart_home_bot", "Добро пожаловать в умный дом.")

	postUpdate(setting_mode_alarm, OFF)
	postUpdate(setting_voice, OFF)
	
	sendCommand(mqtt_pub_esp0, "status") 
	//Thread::sleep(250)
	
end

rule "MQTT sub esp0"
when
	Item mqtt_sub_esp0 received update
then
	var json = mqtt_sub_esp0.state.toString
	
	var id = transform("JSONPATH","$.bt_relay",json)
	var state = transform("JSONPATH","$.state",json)
	
	/* update ESP led status */
	var dim = transform("JSONPATH","$.led",json)
	if(dim != null) postUpdate("setting_led", dim)
	
	if(hall_light_mode_sos.state.toString == "ON")
	{
		if((id != null) && (state != null)) sendCommand(hall_light_mode_sos, OFF)
	}
	else if(setting_mode_alarm.state.toString == "ON")
	{
		if(panic_flag)
		{
			if((id != null) && (state != null)) sendCommand(setting_mode_alarm, OFF)
		}
		else
		{
			if(timer==null)
				timer = createTimer(now.plusSeconds(10)) [|sendTelegram("smart_home_bot", "Тревога. Посторонний в квартире!")] // + сирена
			else
				timer.reschedule(now.plusSeconds(10))
				
			panic_flag = true
			sendCommand(mqtt_pub_esp0, "{\"bt_relay\": 0, \"mode\": \"alarm\"}")
			sendCommand(mqtt_pub_esp0, "{\"bt_relay\": 1, \"mode\": \"alarm\"}")
		}
	}
	else
	{
		/* update light statuses */
		if((id != null) && (state != null)) postUpdate("hall_light_luster_"+id, state)
	}
end

rule "setting_mode_alarm"
when
	Item setting_mode_alarm received command
then
	if(receivedCommand == ON)
	{
		if(hall_light_mode_sos.state.toString == "ON") sendCommand(hall_light_mode_sos, OFF)
		if(setting_voice.state.toString == "ON") say("сигнализация включена")
	}
	else
	{ 
		if(panic_flag == true)
		{
			if(timer!=null) 
			{
				timer.cancel
				timer = null
			}
			panic_flag = false
			sendCommand(mqtt_pub_esp0, "{\"bt_relay\": 0, \"mode\": \"normal\"}")
			sendCommand(mqtt_pub_esp0, "{\"bt_relay\": 1, \"mode\": \"normal\"}")
			sendCommand(mqtt_pub_esp0, "status")
		}
		if(setting_voice.state.toString == "ON") say("сигнализация выключена")
	}
end

rule "hall_light_mode_sos"
when
	Item hall_light_mode_sos received command
then
	if(receivedCommand == ON)
	{
		if(setting_mode_alarm.state.toString == "ON") postUpdate(setting_mode_alarm, OFF)
		sendCommand(mqtt_pub_esp0, "{\"bt_relay\": 0, \"mode\": \"sos\"}")
		sendCommand(mqtt_pub_esp0, "{\"bt_relay\": 1, \"mode\": \"sos\"}")
		sendCommand(mqtt_pub_esp0, "status")
	}
	else
	{
		sendCommand(mqtt_pub_esp0, "{\"bt_relay\": 0, \"mode\": \"normal\"}")
		sendCommand(mqtt_pub_esp0, "{\"bt_relay\": 1, \"mode\": \"normal\"}")
		sendCommand(mqtt_pub_esp0, "status")
	}
end

rule "Setting_voice"
when
	Item setting_voice received command
then
	if(receivedCommand == ON) say("голосовая функция включена")
	else say("голосовая функция выключена")
end

rule "Voice control"
when
	Item VoiceCommand received command
then
	var command = VoiceCommand.state.toString.toLowerCase
	var String item = null
	var String state = null
	var String answer_beg = null
	var String answer = null

	// ON/OFF 
	if(command.contains("вкл"))
	{
		state = "ON"
		answer_beg = "включаю"
	}
	else if(command.contains("выкл"))
	{
		state = "OFF"
		answer_beg = "выключаю"
	}

	// Where
	if(command.contains("зал") && state != null)
	{
		item = "hall_"
	
		// What
		if(command.contains("свет"))
		{
			item += "light_"

			if(command.contains("бра"))
			{
				// TODO
			}
			else if(command.contains("весь"))
			{
				item += "all"
				answer = "весь свет в зале"
			}
			else if(command.contains("холод"))
			{
				item += "luster_0"
				answer = "холодный свет в зале"
			}
			else if(command.contains("тепл"))
			{
				item += "luster_1"
				answer = "теплый свет в зале"
			}
			else 
			{
				item += "luster_all"
				answer = "свет в зале"
			}
		}
		else if(command.contains("подсвет"))
		{
			// TODO
		}
	}
	else if(command.contains("спал") && state != null)
	{
		// TODO
	}

	if(answer == null)
	{
		if(setting_voice.state.toString == "ON") say("неизвестная команда")
	}
	else
	{
		if(setting_voice.state.toString == "ON") say(answer_beg + " " + answer)
		sendCommand(item, state)
	}
end


